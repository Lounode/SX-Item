plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
var baseProject = project(":Module-Base")
var deprecatedProject = project(":Module-Deprecated-v3")
// 全局设置
allprojects { project ->
    apply plugin: 'java'

    group 'io.github.saukiya'
    version '4.0.0'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.build.finalizedBy shadowJar

    repositories {
        mavenLocal()
        mavenCentral()
        //Github Project
        maven { url 'https://jitpack.io' }
        //MythicMobs
        maven { url "https://mvn.lumine.io/repository/maven-public/" }
        //bStats-bukkit
        maven { url 'https://repo.codemc.org/repository/maven-public' }
        //Paper Api
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
    }

    dependencies {
        implementation fileTree('lib')
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        if (project != baseProject) {
            implementation baseProject
        }
    }

    javadoc {
        options.charSet = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'org.openjdk.nashorn:nashorn-core:15.4'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'io.lumine.xikage:MythicMobs:4.12.0'
    compileOnly 'io.lumine:Mythic-Dist:5.2.1'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    implementation 'org.bstats:bstats-bukkit:2.2.1'

//    testImplementation project(':Module-NMS-V1_17_R1')
    rootProject.configurations.compileOnly.dependencies.each { dependency ->
        testImplementation "$dependency.group:$dependency.name:$dependency.version"
        baseProject.dependencies.add('compileOnly', dependency)
    }
}

tasks.register('buildJar', Jar) {
    from deprecatedProject.sourceSets.main.output
    from baseProject.sourceSets.main.output
    from rootProject.sourceSets.main.output
}

jar.archiveClassifier = "base"
processResources.expand(version: version)
buildTask('buildLastJar', "$rootDir/build", "Latest")
buildTask('buildDebugJar', "$rootDir/run/plugins", "Debug")

shadowJar {
    dependsOn buildJar
    // 过滤未使用的依赖
    minimize()
    // bStats重定向
    relocate('org.bstats', 'github.saukiya.sxitem.bstats')
    // 获得子模块的classes文件
    getSubprojects().forEach(subProject -> {
        from subProject.sourceSets.main.output
    })
}

publishing {
    publications {
        maven(MavenPublication) {
//            from components.java
//            artifact jar
            artifact shadowJar
            artifact buildJar
            artifactId = project.name.toLowerCase()
        }
    }
    repositories {
        if (System.getenv("GITHUB_ACTIONS") != null) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/saukiya/SX-Item")
                credentials {
                    username = System.getenv("GITHUB_NAME")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        } else {
            mavenLocal()
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/docs/javadoc")
    source = files(rootProject.sourceSets.main.allJava, baseProject.sourceSets.main.allJava, baseProject.sourceSets.main.allJava)
    classpath = files(rootProject.sourceSets.main.compileClasspath, baseProject.sourceSets.main.compileClasspath, baseProject.sourceSets.main.compileClasspath)
}

def buildTask(String taskName, String destinationDir, String jarSuffix) {
    tasks.register(taskName, Copy) {
        dependsOn build, shadowJar
        from shadowJar.archiveFile.get().asFile.path
        into(destinationDir).rename { "${project.name}-${jarSuffix}.jar" }
    }
}