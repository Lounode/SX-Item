plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
var baseProject = project(":Module-Base")
var depProject = project(":Module-Deprecated-v3")
// 全局设置
allprojects { project ->
    apply plugin: 'java'

    group 'io.github.saukiya'
    version '4.0.0'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.build.finalizedBy shadowJar
    javadoc.enabled = false

    repositories {
        mavenLocal()
        mavenCentral()
        // Spigot Api
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/releases/' }
//        // Paper Api
//        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        // MythicMobs
        maven { url "https://mvn.lumine.io/repository/maven-public/" }
        // bStats-bukkit
        maven { url 'https://repo.codemc.org/repository/maven-public' }
        // Github Project
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        if (project != baseProject) {
            implementation baseProject
        }
        implementation fileTree('lib')
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
    }
}

configure([rootProject, baseProject, depProject]) {
    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
        compileOnly 'org.openjdk.nashorn:nashorn-core:15.4'
        compileOnly 'commons-lang:commons-lang:2.6'
        compileOnly 'io.lumine.xikage:MythicMobs:4.12.0'
        compileOnly 'io.lumine:Mythic-Dist:5.2.1'
        compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
        implementation 'org.bstats:bstats-bukkit:2.2.1'

        //testImplementation project(':Module-NMS-V1_17_R1')
        rootProject.configurations.compileOnly.dependencies.each { dependency ->
            testImplementation "$dependency.group:$dependency.name:$dependency.version"
            baseProject.dependencies.add('compileOnly', dependency)
        }
    }
    javadoc {
        enabled = true
        options.charSet = 'UTF-8'
        options.addStringOption('Xdoclint:none', '-quiet')
        options.links 'https://hub.spigotmc.org/javadocs/spigot/'
        destinationDir = file("${buildDir}/docs/javadoc")
        source = files(rootProject.sourceSets.main.allJava, baseProject.sourceSets.main.allJava, depProject.sourceSets.main.allJava)
        classpath = files(rootProject.sourceSets.main.compileClasspath, baseProject.sourceSets.main.compileClasspath, depProject.sourceSets.main.compileClasspath)
    }
}
tasks.register('mergeJar', Jar) {
    from rootProject.sourceSets.main.output
    from baseProject.sourceSets.main.output
    from depProject.sourceSets.main.output
}

shadowJar {
    dependsOn mergeJar
    // 过滤未使用的依赖
    minimize()
    // bStats重定向
    relocate('org.bstats', 'github.saukiya.sxitem.bstats')
    // 获得子模块的classes文件
    getSubprojects().forEach(subProject -> {
        from subProject.sourceSets.main.output
    })
}

jar.archiveClassifier = "base"
buildTask('buildLatestJar', "$rootDir/build", "Latest")
buildTask('buildDebugJar', "$rootDir/run/plugins", "Debug")
processResources.expand(version: version)

publishing {
    publications {
        maven(MavenPublication) {
//            from components.java
//            artifact jar
            artifact shadowJar
            artifact mergeJar
            artifactId = project.name.toLowerCase()
        }
    }
    repositories {
        if (System.getenv("GITHUB_ACTIONS") != null) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/saukiya/SX-Item")
                credentials {
                    username = System.getenv("GITHUB_NAME")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        } else {
            mavenLocal()
        }
    }
}

def buildTask(String taskName, String destinationDir, String jarSuffix) {
    tasks.register(taskName, Copy) {
        dependsOn build, shadowJar
        from shadowJar.archiveFile.get().asFile.path
        into(destinationDir).rename { "${project.name}-${jarSuffix}.jar" }
    }
}