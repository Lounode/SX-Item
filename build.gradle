plugins {
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
// 全局设置
allprojects {
    apply plugin: 'java'

    group 'io.github.saukiya'
    version '3.2.6'

    repositories {
        mavenCentral()
        //Github Project
        maven { url 'https://jitpack.io' }
        //MythicMobs
        maven { url "https://mvn.lumine.io/repository/maven-public/" }
        //bStats-bukkit
        maven { url 'https://repo.codemc.org/repository/maven-public' }
        //Paper Api
        maven { url 'https://repo.papermc.io/repository/maven-public/' }

    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
        testImplementation 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    }
}
// 子项目设置
subprojects {
    dependencies {
        compileOnly fileTree('lib')
        compileOnly getRootProject()
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    compileOnly 'org.openjdk.nashorn:nashorn-core:15.4'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'io.lumine.xikage:MythicMobs:4.12.0'
    compileOnly 'io.lumine:Mythic-Dist:5.2.1'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    implementation 'org.bstats:bstats-bukkit:2.2.1'

    testImplementation 'org.spigotmc:spigot-api:1.21.1-R0.1-SNAPSHOT'
    testImplementation 'commons-lang:commons-lang:2.6'
}

shadowJar {
    // 过滤未使用的依赖
    minimize()
    // bStats重定向
    relocate('org.bstats', 'github.saukiya.sxitem.bstats')
    //执行所有子项目的 classes task
    dependsOn getSubprojects().stream().map(sub -> sub.getPath() + ':classes').toList()
    // 获得子模块的classes文件
    getSubprojects().forEach(sub -> {
        from(sub.getBuildDir().toString() + '/classes/java/main')
    })
}

//构建并拷贝
task buildLastJar(type: Copy) {
    dependsOn shadowJar
    from shadowJar.archiveFile.get().asFile.path
    into("$rootDir/build").rename { "${project.name}-Latest.jar" }

}
//本地测试服务器
task buildDebugJar(type: Copy) {
    dependsOn shadowJar
    from shadowJar.archiveFile.get().asFile.path
    into("$rootDir/run/plugins").rename { "${project.name}-Debug.jar" }
}

//需要对资源文件中含有的'$'进行转义: $ -> \$
processResources {
    expand(version: version)
}

if (System.getenv("GITHUB_ACTIONS") != null) {
    publishing {
        repositories {
            mavenLocal()
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/saukiya/SX-Item")
                credentials {
                    username = System.getenv("GITHUB_NAME")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.name.toLowerCase()
            }
        }
    }
}